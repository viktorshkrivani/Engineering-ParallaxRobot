' Robotics with the Boe-Bot - EepromNavigationWithWordValues.bs2
' Store lists of word values that dictate.
' {$STAMP BS2}
' {$PBASIC 2.5}]

DEBUG "Program Running!"

' -----[ Variables ]----------------------------------------------------------

counter       VAR Word
pulseCount    VAR Word   ' Stores number of pulses
addressOffset VAR Word   ' Stores offset from label.
instruction   VAR Word   ' Stores EEPROM instruction
pulseRight    VAR Word   ' Stores servo pulse widths
pulseLeft     VAR Word

' -----[ EEPROM Data ]--------------------------------------------------------

' addressOffset     0         2         4         6        8
Pulses_Count  DATA   Word 64,  Word 24,  Word 24,  Word 64, Word 0
Pulses_Left   DATA   Word 850, Word 650, Word 850, Word 650
Pulses_Right  DATA   Word 650, Word 650, Word 850, Word 850

' -----[ Initialization ]-----------------------------------------------------

FREQOUT 4, 2000, 3000 ' Signal program start/reset.

' -----[ Main Routine ]-------------------------------------------------------

DO
  READ Pulses_Count + addressOffset, Word pulseCount
  READ Pulses_Left + addressOffset, Word pulseLeft
  READ Pulses_Right + addressOffset, Word pulseRight

  addressOffset = addressOffset + 2

  FOR counter = 1 TO pulseCount
    PULSOUT 13, pulseLeft
    PULSOUT 12, pulseRight
    PAUSE 20
  NEXT

LOOP UNTIL (pulseCount = 0)

END
